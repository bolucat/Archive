; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "v2rayA"
#define MyAppVersion "TheRealVersion"
#define MyAppPublisher "v2rayA Organization"
#define MyAppURL "https://v2raya.org/"
#define MyAppExeName "v2raya.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B991C73A-599E-460D-A35A-D1ED414DADBF}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ArchitecturesAllowed=TheRealArch
ArchitecturesInstallIn64BitMode=TheRealArch
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=false
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=D:\LICENSE.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=D:\
OutputBaseFilename=installer_windows_inno_TheRealArch
Compression=lzma
SolidCompression=yes
UninstallDisplayName={#MyAppName}-{#MyAppVersion}
WizardStyle=modern
SetupIconFile=D:\v2raya.ico
MinVersion=10.0.14393

[Languages]
Name: "chinesesimplified"; MessagesFile: "ChineseSimplified.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "D:\v2raya-TheRealArch-windows\bin\{#MyAppExeName}"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "D:\v2raya-TheRealArch-windows\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\v2raya-TheRealArch-windows\data\*"; DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\v2raya.ico"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commondesktop}\v2rayA Web Panel"; Filename: "http://localhost:2017"; IconFilename: "{app}\v2raya.ico";
Name: "{group}\v2rayA Web Panel"; Filename: "http://localhost:2017"; IconFilename: "{app}\v2raya.ico";
Name: "{group}\v2rayA Wiki"; Filename: "{#MyAppURL}";
Name: "{group}\{cm:UninstallProgram} {#MyAppName}"; Filename: "{uninstallexe}";

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "V2RAYA_WIN_ENVFILE"; ValueData: "{app}\v2rayA_env.txt"; Flags: uninsdeletevalue

[Run]
; 创建服务
Filename: "{sys}\sc.exe"; Parameters: "create v2rayA binPath= ""{app}\bin\v2raya.exe"" DisplayName= ""v2rayA Service"" start= auto"; Flags: runhidden waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "description v2rayA ""v2rayA - A web GUI client of Project V"""; Flags: runhidden waituntilterminated
; 启动服务
Filename: "{sys}\sc.exe"; Parameters: "start v2rayA"; Flags: runhidden waituntilterminated

[UninstallRun]
; 停止并删除服务
Filename: "{sys}\sc.exe"; Parameters: "stop v2rayA"; Flags: runhidden
Filename: "{sys}\timeout.exe"; Parameters: "/t 2 /nobreak"; Flags: runhidden waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "delete v2rayA"; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}\bin\v2raya.log"

[Code]
const
  EnvironmentKey = 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment';

function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
  BinPath: string;
begin
  BinPath := ExpandConstant('{app}\bin');
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, 'Path', OrigPath) then
  begin
    Result := True;
    exit;
  end;
  Result := Pos(';' + Uppercase(BinPath) + ';', ';' + Uppercase(OrigPath) + ';') = 0;  
  if Result = True then
    Result := Pos(';' + Uppercase(BinPath) + '\;', ';' + Uppercase(OrigPath) + ';') = 0; 
end;

procedure AddBinToPath();
var
  OrigPath: string;
  BinPath: string;
  NewPath: string;
begin
  BinPath := ExpandConstant('{app}\bin');
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, 'Path', OrigPath) then
  begin
    NewPath := BinPath;
  end
  else
  begin
    if NeedsAddPath('') then
      NewPath := OrigPath + ';' + BinPath
    else
      NewPath := OrigPath;
  end;
  
  if NewPath <> OrigPath then
    RegWriteStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, 'Path', NewPath);
end;

procedure RemoveBinFromPath();
var
  OrigPath: string;
  BinPath: string;
  NewPath: string;
  PathList: TStringList;
  I: Integer;
begin
  BinPath := ExpandConstant('{app}\bin');
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, 'Path', OrigPath) then
    exit;
    
  PathList := TStringList.Create;
  try
    PathList.Delimiter := ';';
    PathList.StrictDelimiter := True;
    PathList.DelimitedText := OrigPath;
    
    for I := PathList.Count - 1 downto 0 do
    begin
      if Uppercase(PathList[I]) = Uppercase(BinPath) then
        PathList.Delete(I);
    end;
    
    NewPath := PathList.DelimitedText;
    if NewPath <> OrigPath then
      RegWriteStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, 'Path', NewPath);
  finally
    PathList.Free;
  end;
end;

procedure CreateEnvConfigFile();
var
  EnvFilePath: string;
  EnvContent: TStringList;
begin
  EnvFilePath := ExpandConstant('{app}\v2rayA_env.txt');
  
  // 仅在文件不存在时创建
  if not FileExists(EnvFilePath) then
  begin
    EnvContent := TStringList.Create;
    try
      EnvContent.Add('V2RAYA_V2RAY_ASSETSDIR=' + ExpandConstant('{app}\data'));
      EnvContent.Add('V2RAYA_LOG_FILE=%TEMP%\v2raya.log');
      EnvContent.SaveToFile(EnvFilePath);
    finally
      EnvContent.Free;
    end;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    CreateEnvConfigFile();
    AddBinToPath();
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usPostUninstall then
  begin
    RemoveBinFromPath();
  end;
end;

function InitializeSetup() : Boolean;
var 
  ResultCode: Integer;
begin
  Result := True;
  // 如果服务存在则停止它
  Exec(ExpandConstant('{sys}\sc.exe'), 'stop v2rayA', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  Sleep(2000);
end;